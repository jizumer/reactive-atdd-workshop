plugins {
	id 'org.springframework.boot' version '2.4.3'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'jacoco'
	id 'org.sonarqube' version '3.1.1'
}

group = 'com.thoughtworks'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
	jcenter()
	maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local/' }
}

ext {
	cucumberVersion = '1.2.6'
	extentReportsVersion = '3.1.1'
}

sourceSets {
	integrationTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('backend/src/test/java/com/thoughtworks/reactiveatddworkshop/acceptance')
		}
		resources.srcDir file('backend/src/test/resources')
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
	implementation 'io.springfox:springfox-boot-starter:3.0.0'
	compileOnly 'org.projectlombok:lombok:1.18.16'
	annotationProcessor 'org.projectlombok:lombok:1.18.16'
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'io.r2dbc:r2dbc-h2'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation("com.aventstack:extentreports:$extentReportsVersion")
	testImplementation("com.vimalselvam:cucumber-extentsreport:$extentReportsVersion")
	testImplementation("info.cukes:cucumber-java:$cucumberVersion")
	testImplementation("info.cukes:cucumber-junit:$cucumberVersion")
	testImplementation("info.cukes:cucumber-spring:$cucumberVersion")
	integrationTestCompile(
			'info.cukes:cucumber-core:' + cucumberVersion,
			'info.cukes:cucumber-java:' + cucumberVersion,
			'info.cukes:cucumber-junit:' + cucumberVersion,
			'info.cukes:cucumber-spring:' + cucumberVersion,
			'com.aventstack:extentreports:' + extentReportsVersion,
			'com.vimalselvam:cucumber-extentsreport:' + extentReportsVersion,
			'io.projectreactor:reactor-test',
			'org.springframework.boot:spring-boot-starter-webflux',
			'org.springframework.boot:spring-boot-starter-test'
	)
}

task integrationTest(type: Test) {
	systemProperties = System.properties
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	outputs.upToDateWhen { false }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

test {
	useJUnitPlatform()
}

jacocoTestReport {
	reports {
		xml.enabled true
		html.enabled false
	}
}

sonarqube {
	properties {
		property 'sonar.host.url', 'https://sonarcloud.io'
		property 'sonar.organization', 'thoughtworks-testing'
		property 'sonar.projectKey', 'com.thoughtworks:reactive-atdd-workshop'
	}
}